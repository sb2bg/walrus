use crate::ast::{Node, NodeKind};
use crate::vm::opcode::Opcode;
use crate::span::{Span, Spanned};
use crate::error::{RecoveredParseError, parse_int, parse_float, escape_string};
use lalrpop_util::ErrorRecovery;

grammar;

extern {
    type Error = RecoveredParseError;
}

pub Program: Node = {
    Sp<Statements>,
};

Statements: NodeKind = {
    Statement* => NodeKind::Statements(<>),
};

UnscopedStatements: NodeKind = {
    Statement* => NodeKind::UnscopedStatements(<>),
};

Statement: Node = {
    Sp<If>,
    Sp<For>,
    Sp<While>,
    Sp<Func>,
    <Sp<Expr>> ";",
    <Sp<Assign>> ";",
    <Sp<Return>> ";",
    <Sp<Break>> ";",
    <Sp<Continue>> ";",
    <Sp<Import>> ";",
    <Sp<Print>> ";",
    <Sp<Throw>> ";",
    <Sp<Try>> ";",
    <Sp<Free>> ";",
    <Sp<Defer>> ";",
    <Sp<Extern>> ";",
};

BinOp<Op, Next>: NodeKind = {
    <left: Sp<BinOp<Op, Next>>> <o: Op> <right: Sp<Next>> => NodeKind::BinOp(Box::new(left), o, Box::new(right)),
    Next,
};

Expr = BinOp<OrOp, And>;
And = BinOp<AndOp, Comparison>;
Comparison = BinOp<ComparisonOp, MathExpr>;
MathExpr = BinOp<ExprOp, MathFactor>;
MathFactor = BinOp<FactorOp, Unary>;

Unary = {
    <o: UnaryOp> <e: Sp<Unary>> => NodeKind::UnaryOp(o, Box::new(e)),
    Pow,
};

Pow = BinOp<PowOp, Call>;

Call = {
    <e: Sp<Atom>> <args: Paren<Comma<SpExpr>>> => NodeKind::FunctionCall(Box::new(e), args),
    <e: Sp<Atom>> <args: Bracket<SpExpr>> => NodeKind::Index(Box::new(e), Box::new(args)),
    Atom,
};

OrOp: Opcode = "or" => Opcode::Or;
AndOp: Opcode = "and" => Opcode::And;

ComparisonOp: Opcode = {
    "==" => Opcode::Equal,
    "!=" => Opcode::NotEqual,
    "<" => Opcode::Less,
    ">" => Opcode::Greater,
    "<=" => Opcode::LessEqual,
    ">=" => Opcode::GreaterEqual,
};

ExprOp: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Subtract,
};

FactorOp: Opcode = {
    "*" => Opcode::Multiply,
    "/" => Opcode::Divide,
    "%" => Opcode::Modulo,
};

UnaryOp: Opcode = {
    "-" => Opcode::Negate,
    "not" => Opcode::Not,
};

PowOp: Opcode = {
    "**" => Opcode::Power,
};

Atom: NodeKind = {
    Int,
    Float,
	String,
	Bool,
	Ident,
	List,
	Dict,
	Lambda,
	Void,
	Range,
	Paren<Expr>,
};

// todo: add exponentials and underscores
Int: NodeKind = {
    StrSp<r"\d+"> =>? parse_int(<>, 10),
    StrSp<r"0x[0-9A-Fa-f]+"> =>? parse_int(<>, 16),
    StrSp<r"0o[0-7]+"> =>? parse_int(<>, 8),
    StrSp<r"0b[01]+"> =>? parse_int(<>, 2),
};

Float: NodeKind = {
    StrSp<r"\d+\.\d+"> =>? parse_float(<>),
};

String: NodeKind = {
	StrSp<r#""[^"]*""#> =>? escape_string(<>),
};

Bool: NodeKind = {
	"true" => NodeKind::Bool(true),
	"false" => NodeKind::Bool(false),
};

List: NodeKind = {
    Bracket<Comma<SpExpr>> => NodeKind::List(<>),
};

Dict: NodeKind = {
    Brace<Comma<DictEntry>> => NodeKind::Dict(<>),
};

DictEntry = <SpExpr> ":" <SpExpr>;

Ident: NodeKind = {
	IdentStr => NodeKind::Ident(<>),
};

IdentStr: String = r"[a-zA-Z_][a-zA-Z0-9_]*" => <>.to_string();

If: NodeKind = "if" <c: SpExpr> <b: SpBlock> <e: Else> => NodeKind::If(Box::new(c), Box::new(b), e);

Ternary: NodeKind = {
    <r: SpExpr> "if" <c: SpExpr> "else" <e: SpExpr> => NodeKind::Ternary(Box::new(r), Box::new(c), Box::new(e)),
}

Else: Option<Box<Node>> = {
	"else" <SpBlock> => Some(Box::new(<>)),
	"else" <Sp<If>> => Some(Box::new(<>)),
	=> None,
};

While: NodeKind = {
    "while" <c: SpExpr> <b: SpBlock> => NodeKind::While(Box::new(c), Box::new(b)),
};

For: NodeKind = {
    "for" <v: IdentStr> "in" <i: SpExpr> <b: SpUnscopedBlock> => NodeKind::For(v, Box::new(i), Box::new(b)),
};

Func: NodeKind = {
    "fn" <n: IdentStr> ":" <a: Comma<IdentStr>> <b: SpBlock> => NodeKind::FunctionDefinition(n, a, Box::new(b)),
};

Lambda: NodeKind = {
    ":" <a: Comma<IdentStr>> <b: SpBlock> => NodeKind::AnonFunctionDefinition(a, Box::new(b)),
};

Range: NodeKind = {
    // fixme: limited to identifiers
    <l: SpIdent> ".." <u: SpIdent> => NodeKind::Range(Some(Box::new(l)), Some(Box::new(u))),
    <l: SpIdent> ".." => NodeKind::Range(Some(Box::new(l)), None),
    ".." <u: SpIdent> => NodeKind::Range(None, Some(Box::new(u))),
    ".." => NodeKind::Range(None, None),
};

Void: NodeKind = {
    "void" => NodeKind::Void,
};

Extern: NodeKind = {
    "extern fn" <n: IdentStr> ":" <a: Comma<IdentStr>> => NodeKind::ExternFunctionDefinition(n, a),
};

Assign: NodeKind = {
    "let" <n: IdentStr> "=" <e: SpExpr> => NodeKind::Assign(n, Box::new(e)),
    <n: StrSp<IdentStr>> "=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Equal),
    <n: StrSp<IdentStr>> "+=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Add),
    <n: StrSp<IdentStr>> "-=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Subtract),
    <n: StrSp<IdentStr>> "*=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Multiply),
    <n: StrSp<IdentStr>> "/=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Divide),
    <n: StrSp<IdentStr>> "%=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Modulo),
    <n: StrSp<IdentStr>> "**=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Power),
    <n: StrSp<IdentStr>> "or=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::Or),
    <n: StrSp<IdentStr>> "and=" <e: SpExpr> => NodeKind::Reassign(n, Box::new(e), Opcode::And),
    <n: Sp<Atom>> "[" <i: SpExpr> "]" "=" <e: SpExpr> => NodeKind::IndexAssign(Box::new(n), Box::new(i), Box::new(e)), // fixme: add ops, make x[n][m] work
};

Return: NodeKind = {
    "return" <SpExpr> => NodeKind::Return(Box::new(<>)),
};

Break: NodeKind = {
    "break" => NodeKind::Break,
};

Continue: NodeKind = {
    "continue" => NodeKind::Continue,
};

Import: NodeKind = {
    "import" <m: r#""[^"]*""#> => NodeKind::ModuleImport(m[1..m.len() - 1].to_string(), None),
    "import" <m: r#""[^"]*""#> "as" <a: IdentStr> => NodeKind::ModuleImport(m[1..m.len() - 1].to_string(), Some(a)),
    "import" "@" <p: IdentStr> => NodeKind::PackageImport(p, None),
    "import" "@" <p: IdentStr> "as" <a: IdentStr> => NodeKind::PackageImport(p, Some(a)),
};

Print: NodeKind = {
    "print" <SpExpr> => NodeKind::Print(Box::new(<>)),
    "println" <SpExpr> => NodeKind::Println(Box::new(<>)),
};

Throw: NodeKind = {
    "throw" <SpExpr> => NodeKind::Throw(Box::new(<>)),
};

Try: NodeKind = {
    "try" <b: SpBlock> "catch" <c: IdentStr> <e: SpBlock> => NodeKind::Try(Box::new(b), c, Box::new(e)),
};

Free: NodeKind = {
    "free" <SpExpr> => NodeKind::Free(Box::new(<>)),
};

Defer: NodeKind = {
    "defer" <SpExpr> => NodeKind::Defer(Box::new(<>)),
};

Block = Brace<Statements>;
UnscopedBlock = Brace<UnscopedStatements>;
Brace<T>: T = "{" <T> "}";
Paren<T>: T = "(" <T> ")";
Bracket<T>: T = "[" <T> "]";
Comma<T> = Delim<T, ",">;

SpBlock = Sp<Block>;
SpUnscopedBlock = Sp<UnscopedBlock>;
SpExpr = Sp<Expr>;
SpIdent = Sp<Ident>;

Delim<T, D>: Vec<T> = {
    <mut h: (<T> D)*> <t: T?> => match t {
        Some(t) => {
            h.push(t);
            h
        }
        None => h
    }
};

TrailingDelim<T, D>: Vec<T> = {
    <mut h: (<T> D)*> <t: T> D => {
        h.push(t);
        h
    }
};

Sp<Rule>: Node = <l: @L> <rule: Rule> <r: @R> => Node::new(rule, Span(l, r));
StrSp<Rule>: Spanned<String> = <l: @L> <rule: Rule> <r: @R> => Spanned::new(rule.to_string(), Span(l, r));